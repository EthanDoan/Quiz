apply plugin: 'com.android.application'


def static getGitVersion() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable th) {
        th.printStackTrace()
        return ""
    }
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.netease.nim.quizgame"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
        buildConfigField "String", "BUILD_DATE", "\"${new Date().toLocaleString()}\""

        multiDexEnabled true
        multiDexKeepProguard file('multidexkeep.pro')

        ndk {
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    signingConfigs {
        debug {
            storeFile file("sign/debug.keystore")
        }

        release {
            storeFile file('sign/release.keystore')
            storePassword 'thisiskeystorepassword'
            keyAlias 'nim_demo'
            keyPassword 'thisiskeypassword'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        release {
            shrinkResources true
            minifyEnabled true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            jniLibs.srcDirs = ['libs']
            assets.srcDirs = ['src/main/assets']
            res.srcDirs = ['src/main/res']
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries false
        jumboMode true
        javaMaxHeapSize "4g"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // support
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'

    // 云信SDK
    implementation 'com.netease.nimlib:player:1.1.2'

    // butter knife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // multi dex
    implementation 'com.android.support:multidex:1.0.2'

    // hotfix
    implementation 'com.netease.nimlib:hotfix:1.1.3'
    implementation("com.squareup.okhttp3:okhttp:3.9.0")
    implementation("com.tencent.tinker:tinker-android-lib:1.9.2")
    implementation("com.tencent.tinker:tinker-android-anno:1.9.2")
    annotationProcessor("com.tencent.tinker:tinker-android-anno:1.9.2")
}
